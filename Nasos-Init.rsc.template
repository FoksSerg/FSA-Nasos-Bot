# ===== NASOS INIT TEMPLATE =====
# Шаблон модуля инициализации системы управления насосом
# ВНИМАНИЕ: Это шаблон! Скопируйте в Nasos-Init.rsc и настройте токены
# Автор: Фокин Сергей Александрович foks_serg@mail.ru
# Дата создания: 15 июня 2025
# Дата изменения: 24 июня 2025
# Версия: 1.7 - Синхронизация с основным модулем после исправления ExpectedStopTime

# Основные переменные системы
:global NasosInitStatus false
:global PoeMainInterface "E5-Nasos"

# Настройки Telegram бота (ТРЕБУЮТ НАСТРОЙКИ!)
# Рабочий бот
# :global BotToken "YOUR_BOT_TOKEN"
# Тестовый бот
:global BotToken "YOUR_BOT_TOKEN"
:global ChatId "YOUR_CHAT_ID"

# Переменные состояния системы
:global PoeActiveTimer; :global PoeStartTime; :global PoeTimerName "Nasos-POE-Timer"; :global NewDuration; :global TelegramHeartbeat; :global LastUpdateId; :global LastStopTime; :global ExpectedStopTime; :global MsgSysPoeDisabled
# Переменная для эмуляции POE статуса в тестовом режиме
:global TestPoeStatus "off"

# Инициализация модуля сообщений
:log warning "Насос - Запуск Nasos-Messages"
/system script run Nasos-Messages

# Проверка и отключение POE при старте системы
:if ([:len [/interface ethernet find where name=$PoeMainInterface]] > 0) do={
    :log warning "Насос - Отключение POE после перезагрузки"
    # Цикл питания для надежного отключения
    /interface ethernet poe power-cycle $PoeMainInterface
    :delay 2s
    # Принудительное отключение POE
    /interface ethernet poe set $PoeMainInterface poe-out=off
    :log warning "Насос - POE отключен"
    # Уведомление в Telegram об отключении
    /tool fetch url=("https://api.telegram.org/bot" . $BotToken . "/sendMessage?chat_id=" . $ChatId . "&text=" . $MsgSysPoeDisabled) keep-result=no
}

# Очистка таймера при инициализации
:if ([:len [/system scheduler find name=$PoeTimerName]] > 0) do={
    /system scheduler remove [find name=$PoeTimerName]
    :log warning ("Насос - Удален таймер: " . $PoeTimerName)
}

# Сброс переменных состояния
:set PoeActiveTimer ""
:set ExpectedStopTime ""

# Установка флага успешной инициализации
:set NasosInitStatus true

:log info "Насос - Глобальные переменные инициализированы" 